let rds = Resources.*[
    Type == 'AWS::RDS::DBInstance'
]

let allowed_resolutions = [
    /\{\{resolve:secretsmanager/,
    /\{\{resolve:ssm-secure/
]

rule make_rds_masterpassword_only_secrets_manager when %rds !empty {
    let rds_direct_strings = %rds[ Properties.MasterPassword is_string ]
    when %rds_direct_strings !empty {
        %rds_direct_strings.Properties.MasterPassword in %allowed_resolutions
    }

}

rule make_rds_masterpassword_from_secrets_manger_fn_join when %rds !empty {
    let rds_with_fn_joins = some %rds.Properties.MasterPassword.'Fn::Join'
    when %rds_with_fn_joins !empty {
        %rds_with_fn_joins[0] in %allowed_resolutions or 
        %rds_with_fn_joins[1][0] in %allowed_resolutions
    }
}
